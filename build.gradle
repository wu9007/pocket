apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'jdepend'
apply plugin: "jacoco"
apply plugin: 'build-dashboard'
apply plugin: 'maven-publish'

group = 'org.hunter'
version = '0.0.1-PRE-RELEASE'
sourceCompatibility = 1.8

compileJava {
    options.encoding = 'UTF-8'
}

compileTestJava {
    options.encoding = 'UTF-8'
}

jar {
    manifest {
        attributes 'Implementation-Title': 'pocket',
                'Implementation-Version': version
    }

    exclude "org/hunter/Application.class"
    exclude "org/hunter/demo"
    exclude "application*.yml"
    exclude "pocket.sql"
}

javadoc {
    options.encoding = 'UTF-8'
}

test {
    exclude '*'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom.withXml {
                asNode().remove(asNode().get("dependencies"))
                def dependenciesNode = asNode().appendNode("dependencies")
                def dependencyNode = dependenciesNode.appendNode('dependency')
                dependencyNode.appendNode('groupId', 'org.apache.commons')
                dependencyNode.appendNode('artifactId', 'commons-lang3')
                dependencyNode.appendNode('version', '3.8.1')
            }
        }
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

compileJava.dependsOn(processResources)

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-web:2.1.1.RELEASE')
    implementation('org.springframework.boot:spring-boot-starter-aop:2.1.1.RELEASE')
    implementation('org.springframework.boot:spring-boot-starter-cache:2.1.1.RELEASE')
    implementation('org.springframework.boot:spring-boot-starter-data-redis:2.1.1.RELEASE')
    implementation('org.apache.commons:commons-lang3:3.8.1')
    implementation ('com.google.code.findbugs:jsr305:3.0.2')
    implementation('mysql:mysql-connector-java:8.0.13')
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor:2.1.1.RELEASE")
    testImplementation('org.springframework.boot:spring-boot-starter-test:2.1.1.RELEASE')
}
